## vlc-edge: based on vlc (official package)

# $Id: PKGBUILD 240440 2015-06-09 09:39:21Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Martin Sandsmark <martin.sandsmark@kde.org>

_name=vlc
pkgname=vlc-edge
pkgver=r2.2.0.git.4902.ge4ea3a0
pkgrel=1
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player (GIT Version; with suffix)"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL2')
depends=('a52dec' 'libdvbpsi' 'libxpm' 'qt4' 'sdl_image'
         'libdvdnav' 'libdvdcss' 'lua' 'libmatroska' 'taglib'
         'libmpcdec' 'ffmpeg' 'faad2' 'libshout' 'libmad'
         'libmpeg2' 'xcb-util-keysyms' 'libtar' 'libxinerama'
         'libmtp' 'twolame' 'libnotify' 'libbluray' 'flac'
         'kdelibs' 'opus' 'gtk2')
makedepends=('aribb24' 'aribb25')
install=vlc.install
source=('git://git.videolan.org/vlc.git')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_name}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_name}"

  sed -i -e 's:truetype/ttf-dejavu:TTF:g' modules/visualization/projectm.cpp # font path
}

build() {
  cd "${srcdir}/${_name}"

  ./bootstrap
  ./configure --prefix=/usr/lib/vlc-edge \
              --sysconfdir=/etc \
              --disable-rpath \
              --enable-nls \
              --enable-lirc=auto \
              --with-kde-solid=no \
#              RCC=/usr/bin/rcc-qt4

  make
}

package() {
  cd "${srcdir}/${_name}"

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/bin
  for b in "${pkgdir}"/usr/lib/vlc-edge/bin/*; do
    ln -s /usr/lib/vlc-edge/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-edge
  done
}
